<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_Logging" Id="{f3c6275a-64fe-47c7-8398-c8b4638ef175}" SpecialFunc="None">
    <Declaration><![CDATA[// Provide logging 
FUNCTION_BLOCK FB_Logging IMPLEMENTS I_Logging
VAR_INPUT
	eLogLevel:				E_LogLevel:= E_LogLevel.Debug;
END_VAR
VAR_OUTPUT
    bInfo:					BOOL;
    bWarn:					BOOL;
    bError:					BOOL;
    bFatal:					BOOL;
END_VAR
VAR
	aMessages:				ARRAY[0..99] OF ST_LogMessage; // Message store
    nMessages:				UINT := 0;                     // Message count
	{attribute 'hide'} 
	nIndex: 				UINT;
	iLogging:				I_Logging;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// remove outdated messages
nIndex := 0;
WHILE nIndex < nMessages DO
	IF aMessages[nIndex].bActive THEN
		aMessages[nIndex].bActive:= FALSE;
		nIndex := nIndex + 1;
	ELSE
        MEMMOVE(ADR(aMessages[nIndex]), ADR(aMessages[nIndex + 1]), SIZEOF(ST_LogMessage) * (nMessages - nIndex));
        nMessages := nMessages - 1;
		bInfo:=	FALSE;
		bWarn:=	FALSE;
		bError:= FALSE;
		bFatal:= FALSE;
    END_IF
END_WHILE]]></ST>
    </Implementation>
    <Method Name="M_Attach" Id="{f57e9ab0-821c-4fb8-ac2e-06aed1be4c34}">
      <Declaration><![CDATA[METHOD PUBLIC M_Attach : BOOL
VAR_INPUT
	iLogging:				I_Logging;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF THIS^.iLogging = 0 THEN
	THIS^.iLogging:= iLogging;
	M_Attach:= TRUE;
ELSIF THIS^.iLogging = iLogging THEN
	// already inside
	M_Attach:= TRUE;
	RETURN;
ELSE
	// redirect to next logging function block
	THIS^.iLogging.M_Attach(iLogging);
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Log" Id="{ced7a02e-e5d1-4f22-8859-d2eccf7c8c34}">
      <Declaration><![CDATA[METHOD PUBLIC M_Log : BOOL
VAR_INPUT
	eLogLevel:			E_LogLevel;
	nID:				UDINT;
	sSource:			STRING(255);
	sMessage:			STRING(255);
	sArguments:			STRING(255);
END_VAR
VAR
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF THIS^.eLogLevel > eLogLevel THEN
	M_Log:= TRUE;
	RETURN;
END_IF

CASE eLogLevel OF
	E_LogLevel.Info:
    	bInfo:= TRUE;
		
	E_LogLevel.Warn:		
    	bWarn:= TRUE;
		
	E_LogLevel.Error:
    	bError:= TRUE;
		
	E_LogLevel.Fatal:
    	bFatal:= TRUE;
		
	ELSE
		;
END_CASE


// Find
FOR nIndex := 0 TO nMessages DO
    IF aMessages[nIndex].sMessage = sMessage AND_THEN aMessages[nIndex].sSource = sSource THEN
        aMessages[nIndex].bActive:= TRUE;
		M_Log := TRUE;
        RETURN;
    END_IF
END_FOR

// else Add
IF nMessages < 99 THEN
	nIndex := nMessages;
    aMessages[nMessages].eLogLevel:= 	eLogLevel;
    aMessages[nMessages].nID:=			nID;
	aMessages[nMessages].nTimestamp:= 	F_GetSystemTime();
    aMessages[nMessages].sSource:=		sSource;
    aMessages[nMessages].sMessage:=		sMessage;
	aMessages[nMessages].sDefault:=		F_Format(sMessage, sArguments);
    aMessages[nMessages].sArguments:=	sArguments;
	aMessages[nMessages].bActive:=		TRUE;
    nMessages := nMessages + 1;
	// just for new messages
	IF iLogging <> 0 THEN
		iLogging.M_Log(	eLogLevel, nID, sSource, sMessage, sArguments);
	END_IF
	M_Log := TRUE;
END_IF]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>