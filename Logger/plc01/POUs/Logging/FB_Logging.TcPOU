<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_Logging" Id="{f3c6275a-64fe-47c7-8398-c8b4638ef175}" SpecialFunc="None">
    <Declaration><![CDATA[// Provide logging 
FUNCTION_BLOCK FB_Logging IMPLEMENTS I_Logging
VAR_INPUT
	eLogLevel:				E_LogLevel:= E_LogLevel.Verbose;
END_VAR
VAR_OUTPUT
    bInfo:					BOOL;
    bWarning:				BOOL;
    bError:					BOOL;
    bCritical:				BOOL;
END_VAR
VAR
	aMessages:				ARRAY[0..99] OF ST_Message; // Message store
    nMessages:				UINT := 0;                     // Message count
	{attribute 'hide'} 
	nIndex: 				UINT;
	iLogging:				I_Logging;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[nIndex := 0;
WHILE nIndex < nMessages DO
	IF aMessages[nIndex].bActive THEN
		aMessages[nIndex].bActive:= FALSE;
		nIndex := nIndex + 1;
	ELSE
		IF iLogging <> 0 THEN
			// publish outdated messages to remove
			iLogging.M_Log(aMessages[nIndex]);
		END_IF
        MEMMOVE(ADR(aMessages[nIndex]), ADR(aMessages[nIndex + 1]), SIZEOF(ST_Message) * (nMessages - nIndex));
        nMessages := nMessages - 1;
		bInfo:=	FALSE;
		bWarning:=	FALSE;
		bError:= FALSE;
		bCritical:= FALSE;
    END_IF
END_WHILE]]></ST>
    </Implementation>
    <Method Name="M_Attach" Id="{f57e9ab0-821c-4fb8-ac2e-06aed1be4c34}">
      <Declaration><![CDATA[METHOD PUBLIC M_Attach : BOOL
VAR_INPUT
	iLogging:				I_Logging;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF THIS^.iLogging = 0 THEN
	THIS^.iLogging:= iLogging;
	M_Attach:= TRUE;
ELSIF THIS^.iLogging = iLogging THEN
	// already inside
	M_Attach:= TRUE;
	RETURN;
ELSE
	// redirect to next logging function block
	THIS^.iLogging.M_Attach(iLogging);
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Log" Id="{ced7a02e-e5d1-4f22-8859-d2eccf7c8c34}">
      <Declaration><![CDATA[METHOD PUBLIC M_Log : BOOL
VAR_INPUT
	stMessage:			ST_Message;
END_VAR
VAR
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF eLogLevel > stMessage.eLogLevel THEN
	M_Log:= TRUE;
	RETURN;
END_IF

CASE stMessage.eLogLevel OF
	E_LogLevel.Info:
    	bInfo:= TRUE;
		
	E_LogLevel.Warning:		
    	bWarning:= TRUE;
		
	E_LogLevel.Error:
    	bError:= TRUE;
		
	E_LogLevel.Critical:
    	bCritical:= TRUE;
		
	ELSE
		;
END_CASE


// Find
FOR nIndex := 0 TO nMessages DO
    IF aMessages[nIndex].sMessage = stMessage.sMessage AND_THEN aMessages[nIndex].sSource = stMessage.sSource THEN
        aMessages[nIndex].bActive:= TRUE;
		M_Log := TRUE;
        RETURN;
    END_IF
END_FOR

// else Add
IF nMessages < 99 THEN
	nIndex := nMessages;
	stMessage.bActive:= TRUE;
    aMessages[nMessages]:= stMessage;;
    nMessages := nMessages + 1;
	// reduce internal buffers 
	// publish new messages to log
	IF iLogging <> 0 THEN
		iLogging.M_Log(stMessage);
	END_IF
	M_Log := TRUE;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Property Name="P_LogLevel" Id="{5b672b68-2f94-4b6d-a86c-6bbf69121d9a}">
      <Declaration><![CDATA[PROPERTY PUBLIC P_LogLevel : E_LogLevel]]></Declaration>
      <Get Name="Get" Id="{8afc35b6-8665-4f6e-8d3f-84f7aea7e81f}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[P_LogLevel:= eLogLevel;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{319b062e-4082-438b-bd27-4271f016d2a7}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[eLogLevel:= P_LogLevel;
]]></ST>
        </Implementation>
      </Set>
    </Property>
  </POU>
</TcPlcObject>